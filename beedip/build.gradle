apply plugin: 'com.android.application'
apply plugin: 'maven'

def googleMapsApiReleaseKey = hasProperty('RELEASE_MAPS_MAPCACHE_API_KEY') ? RELEASE_MAPS_MAPCACHE_API_KEY: ''
def googleMapsApiKeyDebug = hasProperty('DEBUG_MAPS_API_KEY') ? DEBUG_MAPS_API_KEY: ''

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    dexOptions {
        javaMaxHeapSize "4g"
    }

    defaultConfig {
        applicationId "it.uniurb.beedip"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 18
        versionName '1.00'
        multiDexEnabled true
        //testInstrumentationRunner 'android.test.InstrumentationTestRunner'
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        //testHandleProfiling true
        //testFunctionalTest true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            resValue "string", "GOOGLE_MAPS_ANDROID_API_KEY", googleMapsApiReleaseKey
        }
        debug {
            minifyEnabled false
            resValue "string", "GOOGLE_MAPS_ANDROID_API_KEY", googleMapsApiKeyDebug
            applicationIdSuffix ".debug"
            debuggable true
        }
        staging {
            initWith debug
            manifestPlaceholders = [hostName:"internal.example.com"]
            applicationIdSuffix ".debugStaging"
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
}

task androidAppVersion {
    doLast {
        com.android.build.gradle.AppExtension appExt = project.extensions.android;
        println "versionCode: ${appExt.defaultConfig.applicationId}@${appExt.defaultConfig.versionCode}"
        println "versionName: ${appExt.defaultConfig.applicationId}@${appExt.defaultConfig.versionName}"
    }
}

dependencies {
    compile 'mil.nga.geopackage.map:geopackage-android-map:1.4.1'
    // comment out to build locally
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support:support-annotations:25.2.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
}
configure extensions.android, {
    if (project.hasProperty('RELEASE_STORE_FILE') && new File((String)project.getProperty('RELEASE_STORE_FILE')).exists()) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
        buildTypes {
            release {
                setSigningConfig(signingConfigs.release)
            }
        }
    }
    else {
        println 'no keystore configured'
    }
    testOptions {
        reportDir "$rootDir/test-reports"
        resultsDir "$rootDir/test-results"
    }

}

